buildscript {
    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath 'net.ltgt.gradle:gradle-errorprone-plugin:4.1.0'
        classpath 'com.diffplug.spotless:spotless-plugin-gradle:7.0.3'
    }
}

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'org.jreleaser' version '1.19.0'
}

allprojects {
    group = 'com.arcesium.swiftlake'
    version = '0.2.0'
    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'net.ltgt.errorprone'
    apply plugin: 'com.diffplug.spotless'
    apply plugin: 'org.jreleaser'

    java {
        withJavadocJar()
        withSourcesJar()
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    dependencies {
        errorprone libs.errorprone.core
        compileOnly libs.errorprone.annotations
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.errorprone {
            disableWarningsInGeneratedCode = true
            errorproneArgs.addAll(
                    "-XepExcludedPaths:.*/(test|integration|generated-src|generated)/.*"
            )
        }
    }

    pluginManager.withPlugin('com.diffplug.spotless') {
        spotless {
            java {
                target 'src/main/java/**/*.java', 'src/test/java/**/*.java'
                googleJavaFormat("1.22.0")
                removeUnusedImports()
            }
        }
    }

    tasks.withType(Test) {
        useJUnitPlatform()
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java

                pom {
                    name = 'SwiftLake'
                    description = 'SwiftLake: Simplifying lakehouse data operations with Iceberg and DuckDB'
                    url = 'https://github.com/arcesium/swiftlake'

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id = 'mothukur'
                            name = 'Rama Krishna Mothukuri'
                            email = 'mothukur@arcesium.com'
                        }
                    }

                    scm {
                        connection = 'scm:git:git://github.com/arcesium/swiftlake.git'
                        developerConnection = 'scm:git:ssh://github.com:arcesium/swiftlake.git'
                        url = 'https://github.com/arcesium/swiftlake/tree/main'
                    }
                }
            }
        }
        repositories {
            maven {
                url = layout.buildDirectory.dir('staging-deploy')
            }
        }
    }

    jreleaser {
        signing {
            active = 'ALWAYS'
            armored = true
            mode = 'FILE'
        }
        deploy {
            maven {
                mavenCentral {
                    sonatype {
                        active = 'ALWAYS'
                        applyMavenCentralRules = true
                        url = 'https://central.sonatype.com/api/v1/publisher'
                        stagingRepository('build/staging-deploy')
                    }
                }
            }
        }
    }
}